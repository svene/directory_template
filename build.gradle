//buildscript {
//	apply from: 'gradle/Bintray.gradle' // see https://bitbucket.org/davidmc24/gradle-bintray-plugin
//}

/*
 * create snapshot: gradle makeSnapshot
 * create release: gradle makeRelease
 */

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'

group = 'org.svenehrke'
archivesBaseName = 'directory_template'
version = '0.0.9'

idea {
	project {
		jdkName = '1.6'
	}
	module {
		downloadSources = false
		downloadJavadoc = false
	}
}

repositories {
	mavenCentral()
	mavenRepo (url: 'http://m2repo.spockframework.org/snapshots')
	}

dependencies {
	compile 'org.apache.ant:ant:1.9.0' // to be able to use AntBuilder from gradle commandline (see http://groovy.329449.n5.nabble.com/Do-I-need-to-include-ant-jar-junit-jar-with-groovy-all-td5711082.html)
	compile 'org.slf4j:slf4j-api:1.6.2'
	compile 'ch.qos.logback:logback-core:0.9.30'
	compile 'commons-io:commons-io:2.4'
	compile 'org.eclipse.jgit:org.eclipse.jgit:2.3.1.201302201838-r'

	testCompile "org.spockframework:spock-core:0.7-groovy-1.8"
	testCompile 'junit:junit:4.10'
	groovy 'org.codehaus.groovy:groovy-all:1.8.9'
}


uploadArchives {
	repositories.mavenDeployer {
		repository(url: "${userDeploymentRepoBaseURL}/releases")
	}
}

// see http://evgeny-goldin.com/wiki/Gradle  (section 'Setting the repository URL at the time the DAG is ready')
gradle.taskGraph.whenReady { tg ->
	if (!tg.hasTask(':makeRelease')) {
		version += '-SNAPSHOT'
		uploadArchives.repositories['mavenDeployer'].repository.url = "${userDeploymentRepoBaseURL}/snapshots"
	}
}

task makeSnapshot(dependsOn: 'uploadArchives')

task makeRelease(dependsOn: 'uploadArchives')

task wrapper(type: Wrapper) {
	gradleVersion = '1.5'
}

